
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>Homework 3</title>
		<meta charset="utf-8">
		<style>
			body {
			  	margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
    <body>

        <div id="container"></div>

        <!-- External Scripts -->

        <script src="js/three.js"></script>

        <!-- Vertex Shader -->

        <script id="vertexShader" type="x-shader/x-vertex">

	        uniform mat4 modelMatrix;
	        uniform mat4 viewMatrix;
            uniform mat4 projectionMatrix;

            attribute vec3 position;

            varying vec3 v_pos;

	        void main() {

                v_pos = position;
                gl_Position = projectionMatrix * viewMatrix * modelMatrix * vec4(position, 1.0);

            }

	    </script>

        <!-- Fragment Shader -->

	    <script id="fragmentShader" type="x-shader/x-fragment">

            precision mediump float;

            const int MAX_MARCH_STEPS = 300;
            const float EPSILON = 0.0001;
            const float START = 0.0;
            const float END = 300.0;

            uniform vec2 resolution;
            uniform float timer;

            varying vec3 v_pos;

            // Taken from the lab, and adapted from Shadertoy example: https://www.shadertoy.com/view/4tcGDr
            mat3 rotateX(float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat3(
                    vec3( 1,  0,  0),
                    vec3( 0,  c, -s),
                    vec3( 0,  s,  c)
                );
            }

            mat3 rotateY(float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat3(
                    vec3( c,  0,  s),
                    vec3( 0,  1,  0),
                    vec3(-s,  0,  c)
                );
            }

            mat3 rotateZ(float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat3(
                    vec3( c, -s,  0),
                    vec3( s,  c,  0),
                    vec3( 0,  0,  1)
                );
            }

            float intersectSDF(float distA, float distB) {
                return max(distA, distB);
            }

            float unionSDF(float distA, float distB) {
                return min(distA, distB);
            }

            float differenceSDF(float distA, float distB) {
                return max(distA, -distB);
            }

            float boxSDF(vec3 samplePoint, vec3 size) {

                vec3 dist = abs(samplePoint) - (size / 2.0);

                float insideDistance = min(max(dist.x, max(dist.y, dist.z)), 0.0);
                float outsideDistance = length(max(dist, 0.0));

                return insideDistance + outsideDistance;

            }   

            float sphereSDF(vec3 samplePoint, float radius) {

                return length(samplePoint) - radius;

            }

            float cylinderSDF(vec3 samplePoint, float height, float radius) {
            
                float inOutRadius = length(samplePoint.xy) - radius;
                float inOutHeight = abs(samplePoint.z) - (height / 2.0);
                float insideDistance = min(max(inOutRadius, inOutHeight), 0.0);
                float outsideDistance = length(max(vec2(inOutRadius, inOutHeight), 0.0));

                return insideDistance + outsideDistance;

            }

            float sceneSDF(vec3 samplePoint) {

                samplePoint = rotateY(timer / 2.0) * samplePoint;

                float cylinderRadius = 0.4 + ((1.0 - 0.4) * (1.0 + sin(1.7 * timer)) / 2.0);
                float cylinder1 = cylinderSDF(samplePoint, 2.0, cylinderRadius);
                float cylinder2 = cylinderSDF(rotateX(radians(90.0)) * samplePoint, 2.0, cylinderRadius);
                float cylinder3 = cylinderSDF(rotateY(radians(90.0)) * samplePoint, 2.0, cylinderRadius);

                float cube = boxSDF(samplePoint, vec3(1.8, 1.8, 1.8));
                float sphere = sphereSDF(samplePoint, 1.2);

                float ballOffset = 0.4 + 1.0 + sin(1.7 * timer);
                float ballRadius = 0.3;
                float balls = sphereSDF(samplePoint - vec3(ballOffset, 0.0, 0.0), ballRadius);
                balls = unionSDF(balls, sphereSDF(samplePoint + vec3(ballOffset, 0.0, 0.0), ballRadius));
                balls = unionSDF(balls, sphereSDF(samplePoint - vec3(0.0, ballOffset, 0.0), ballRadius));
                balls = unionSDF(balls, sphereSDF(samplePoint + vec3(0.0, ballOffset, 0.0), ballRadius));
                balls = unionSDF(balls, sphereSDF(samplePoint - vec3(0.0, 0.0, ballOffset), ballRadius));
                balls = unionSDF(balls, sphereSDF(samplePoint + vec3(0.0, 0.0, ballOffset), ballRadius));

                float csgNut = differenceSDF(intersectSDF(cube, sphere), unionSDF(cylinder1, unionSDF(cylinder2, cylinder3)));

                return unionSDF(balls, csgNut);
            }

            vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {

                vec2 xy = fragCoord;
                float z = size.y / tan(radians(fieldOfView) / 2.0);
                return normalize(vec3(xy, -z));

            }

            mat3 rayMarchViewMatrix(vec3 cam, vec3 center, vec3 up) {

                vec3 f = normalize(center - cam);
                vec3 s = normalize(cross(f, up));
                vec3 u = cross(s, f);
                return mat3(s, u, -f);

            }

            float rayMarch(vec3 cam, vec3 dir, float start, float end) { // aka shortestDistancetoSurface()

                float step = start;

                for (int i = 0; i < MAX_MARCH_STEPS; i++) {

                    float dist = sceneSDF(cam + step * dir);

                    if (dist < EPSILON) return step; // if inside the geometry

                    step += dist;
                    if (step >= end) return end;

                }
            
                return end;

            }

            vec3 estimateNormal(vec3 p) {
                return normalize(vec3 (
                    sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
                    sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
                    sceneSDF(vec3(p.x, p.y, p.z + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
                ));
            }

            vec3 phongLight(vec3 diffuse, vec3 specular, float alpha, vec3 pos,
                            vec3 cam, vec3 lightPos, vec3 lightIntensity) {

                vec3 N = estimateNormal(pos);
                vec3 L = normalize(lightPos - pos);
                vec3 V = normalize(cam - pos);
                vec3 R = normalize(reflect(-L, N));

                float dotLN = dot(L, N);
                float dotRV = dot(R, V);

                if (dotLN < 0.0) {
                    
                    return vec3(0.0, 0.0, 0.0);

                }
            
                if (dotRV < 0.0) {
            
                    return lightIntensity * (diffuse * dotLN);

                }

                return lightIntensity * (diffuse * dotLN + specular * pow(dotRV, alpha));

            }

            vec3 phongIllumination(vec3 ambient, vec3 diffuse, vec3 specular, float alpha, vec3 pos, vec3 cam) {

                const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);

                vec3 color = ambientLight * ambient;
                vec3 lightAPos = vec3(4.0 * sin(timer), 2.0, 4.0 * cos(timer));
                vec3 lightAIntensity = vec3(0.4, 0.4, 0.4);

                color += phongLight(diffuse, specular, alpha, pos, cam, lightAPos, lightAIntensity);

                vec3 lightBPos = vec3(2.0 * sin(0.37 * timer), 2.0 * cos(0.37 * timer), 2.0);
                vec3 lightBIntensity = vec3(0.4, 0.4, 0.4);
            
                color += phongLight(diffuse, specular, alpha, pos, cam, lightBPos, lightBIntensity);

                return color;

            }

            void main() { // aka mainImage()

                vec3 cam = vec3(8.0, 5.0 * sin(0.2 * timer), 7.0);
                vec3 viewDir = rayDirection(45.0, resolution, v_pos.xy);

                mat3 viewToWorld = rayMarchViewMatrix(cam, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));
                vec3 worldDir = viewToWorld * viewDir;

                float dist = rayMarch(cam, worldDir, START, END);

                if (dist > END - EPSILON) {
                    
                    gl_FragColor = vec4(0.0, 0.0, 0.2, 0.0);
                    return;

                }

                vec3 pos = cam + dist * worldDir;

                vec3 ambient  = (estimateNormal(pos) + vec3(1.0)) / 2.0;
                vec3 diffuse  = ambient;
                vec3 specular = vec3(1.0, 1.0, 1.0);
                float shininess = 10.0;

                vec3 color = phongIllumination(ambient, diffuse, specular, shininess, pos, cam);

                gl_FragColor = vec4(color, 1.0);
                
	        }

	    </script>

        <!-- Main Script -->

	    <script>

            // scene set-up

            var width = window.innerWidth;
            var height = window.innerHeight;

            var camera = new THREE.OrthographicCamera(width / -2, width / 2, height / 2, height / -2, 0.1, 1000);
            camera.position.z = 1.0;
            var scene = new THREE.Scene();

            var renderer = new THREE.WebGLRenderer();
            renderer.setSize(width, height);
            document.body.appendChild(renderer.domElement);

            // plane geometry and quad

            var planeSDF = new THREE.PlaneGeometry(width, height);

            var planeInput = {
                resolution: { type: "v2", value: new THREE.Vector2(width, height) },
                timer: { type: "f", value: 0.0 },
            };

            var planeMaterial = new THREE.RawShaderMaterial({
                uniforms: planeInput,
                vertexShader: document.getElementById('vertexShader').innerHTML,
                fragmentShader: document.getElementById('fragmentShader').innerHTML
            });

            var fullScreenQuad = new THREE.Mesh(planeSDF, planeMaterial);

            scene.add(fullScreenQuad);

            // render

            animate();

            function animate() {

                requestAnimationFrame(animate);
                render();

            }

            function render() {
                
                fullScreenQuad.material.uniforms.timer.value += 0.025;

                renderer.render(scene, camera);

	        }

		</script>

	</body>
</html>

